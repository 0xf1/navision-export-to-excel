OBJECT Codeunit 50150 recordset no-sql
{
  OBJECT-PROPERTIES
  {
    Date=16.10.21;
    Time=[ 4:18:17];
    Modified=Yes;
    Version List=[excel];
  }
  PROPERTIES
  {
    OnRun=BEGIN
            export;
          END;

  }
  CODE
  {

    PROCEDURE export@1210000();
    VAR
      item@1210000 : Record 27;
      xl@1210002 : Automation "{00020813-0000-0000-C000-000000000046} 1.9:{00024500-0000-0000-C000-000000000046}:'Microsoft Excel 16.0 Object Library'.Application";
      dlg@1210005 : Dialog;
      dlgCounter@1210006 : Integer;
      itemCount@1210007 : Integer;
      rs@1210009 : Automation "{2A75196C-D9EB-4129-B803-931327F72D5C} 2.8:{00000535-0000-0010-8000-00AA006D2EA4}:'Microsoft ActiveX Data Objects 2.8 Library'.Recordset";
      rsVar@1210008 : Variant;
      i@1210001 : Integer;
    BEGIN
      CREATE(xl, TRUE);
      xl.Visible:= TRUE;
      xl.Workbooks.Add;

      // create recordset schema. Ref. at https://www.w3schools.com/asp/prop_para_type.asp
      CREATE(rs);
      rs.Fields.Append('Item No.',8); // text
      rs.Fields.Append('Description', 8); // text
      rs.Fields.Append('Unit Price', 5); // double
      rs.Fields.Append('Gross Weight', 5); // double
      rs.Fields.Append('Net Weight', 5); // double
      rs.Fields.Append('Last Date Modified', 133); // date
      rs.Open;

      // header
      FOR i := 0 TO rs.Fields.Count - 1 DO
        xl.Range('A1').Offset(0,i).Value := rs.Fields.Item(i).Name;

      dlg.OPEN('Exporting data @1@@@@@@@@@@@@@@@@@@@@@@@');
      itemCount := item.COUNT;

      // collect data
      IF item.FINDSET THEN
      REPEAT
        dlgCounter += 1;
        dlg.UPDATE(1, ROUND(dlgCounter * 10000 / itemCount, 1));
        rs.AddNew;
        rs.Fields.Item('Item No.').Value := item."No.";
        rs.Fields.Item('Description').Value := item.Description + item."Description 2";
        rs.Fields.Item('Unit Price').Value := FORMAT(item."Unit Price", 0, '<Standard Format,2>');
        rs.Fields.Item('Gross Weight').Value := FORMAT(item."Gross Weight", 0, '<Standard Format,2>');
        rs.Fields.Item('Net Weight').Value := FORMAT(item."Net Weight", 0, '<Standard Format,2>');
        IF item."Last Date Modified" <> 0D THEN
          rs.Fields.Item('Last Date Modified').Value := item."Last Date Modified";
      UNTIL item.NEXT = 0;

      // flush collected data to excel
      rsVar := rs;
      xl.Range('A2').CopyFromRecordset(rsVar);

      // borders, formatting
      xl.Range('A1').Resize(itemCount+1, rs.Fields.Count).Borders.LineStyle := 1;
      xl.Range('A1').EntireRow.Font.Bold := TRUE;
      xl.Columns.AutoFit;
    END;

    BEGIN
    END.
  }
}

