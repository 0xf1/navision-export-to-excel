OBJECT Codeunit 50151 regular-slow-export
{
  OBJECT-PROPERTIES
  {
    Date=17.10.21;
    Time=14:14:11;
    Modified=Yes;
    Version List=[excel];
  }
  PROPERTIES
  {
    OnRun=BEGIN
            export;
          END;

  }
  CODE
  {

    PROCEDURE export@1210000();
    VAR
      item@1210000 : Record 27;
      xl@1210002 : Automation "{00020813-0000-0000-C000-000000000046} 1.9:{00024500-0000-0000-C000-000000000046}:'Microsoft Excel 16.0 Object Library'.Application";
      dlg@1210005 : Dialog;
      dlgCounter@1210006 : Integer;
      itemCount@1210007 : Integer;
      lineNo@1210001 : Integer;
      fieldCount@1210003 : Integer;
    BEGIN
      CREATE(xl, TRUE);
      xl.Visible:= TRUE;
      xl.Workbooks.Add;

      // header
      xl.Range('A1').Value := 'Item No.';
      xl.Range('B1').Value := 'Description';
      xl.Range('C1').Value := 'Unit Price';
      xl.Range('D1').Value := 'Gross Weight';
      xl.Range('E1').Value := 'Net Weight';
      xl.Range('F1').Value := 'Last Date Modified';
      fieldCount := 6;

      dlg.OPEN('Exporting data @1@@@@@@@@@@@@@@@@@@@@@@@');
      itemCount := item.COUNT;

      // data
      lineNo := 1;
      IF item.FINDSET THEN
      REPEAT
        lineNo += 1;
        dlgCounter += 1;
        dlg.UPDATE(1, ROUND(dlgCounter * 10000 / itemCount, 1));

        xl.Range('A' + FORMAT(lineNo)).Value := item."No.";
        xl.Range('B' + FORMAT(lineNo)).Value := item.Description + item."Description 2";
        xl.Range('C' + FORMAT(lineNo)).Value := FORMAT(item."Unit Price");
        xl.Range('D' + FORMAT(lineNo)).Value := FORMAT(item."Gross Weight");
        xl.Range('E' + FORMAT(lineNo)).Value := FORMAT(item."Net Weight");
        IF item."Last Date Modified" <> 0D THEN
          xl.Range('F' + FORMAT(lineNo)).Value := item."Last Date Modified";
      UNTIL item.NEXT = 0;

      // borders, formatting
      xl.Range('A1').Resize(itemCount+1, fieldCount).Borders.LineStyle := 1;
      xl.Range('A1').EntireRow.Font.Bold := TRUE;
      xl.Columns.AutoFit;
    END;

    BEGIN
    END.
  }
}

